plugins {
    id "com.github.johnrengelman.shadow" version "7.1.2"
    id 'org.jetbrains.kotlin.jvm'
}

architectury {
    platformSetupLoomIde()
    forge()
}

loom {
    accessWidenerPath = project(":common").loom.accessWidenerPath

    forge {
        convertAccessWideners = true
        extraAccessWideners.add loom.accessWidenerPath.get().asFile.name
    }
}

configurations {
    common
    shadowCommon // Don't use shadow from the shadow plugin because we don't want IDEA to index this.
    compileClasspath.extendsFrom common
    runtimeClasspath.extendsFrom common
    developmentForge.extendsFrom common
}

repositories {
    mavenCentral()
    // No additional repositories are needed on forge, Tterrag's has it all.
}

dependencies {
    forge("net.minecraftforge:forge:$minecraft_version-$forge_version")
    common(project(path: ":common", configuration: "namedElements")) { transitive = false }
    shadowCommon(project(path: ":common", configuration: "transformProductionForge")) { transitive = false }

    // Create and its dependencies
    modImplementation("com.simibubi.create:create-${minecraft_version}:${create_forge_version}:slim") { transitive = false }
    modImplementation("net.createmod.ponder:Ponder-Forge-${minecraft_version}:${ponder_version}")
    modImplementation("com.tterrag.registrate:Registrate:${registrate_forge_version}")
    modCompileOnly("dev.engine-room.flywheel:flywheel-forge-api-${minecraft_version}:${flywheel_version}")
    modRuntimeOnly("dev.engine-room.flywheel:flywheel-forge-${minecraft_version}:${flywheel_version}")

    compileOnly(annotationProcessor("io.github.llamalad7:mixinextras-common:0.4.1"))
    implementation("io.github.llamalad7:mixinextras-forge:0.4.1")

    // Development QOL
    modLocalRuntime("mezz.jei:jei-$minecraft_version-forge:$jei_version") { transitive = false }
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8"

    // if you would like to add integration with JEI, uncomment this line.
//    modCompileOnly("mezz.jei:jei-$minecraft_version:$jei_version:api")
}

processResources {
    // set up properties for filling into metadata
    Map<String, Object> properties = [
            "version": mod_version,
            "mod_id": mod_id,
            "mod_name": mod_name,
            "mod_description": mod_description,
            "authors": authors,
            "forge_version": forge_version.split("\\.")[0], // only specify major version of forge
            "minecraft_version": minecraft_version,
            "create_version": create_forge_version.split("-")[0] // cut off build number

    ]
    inputs.properties(properties)

    filesMatching("META-INF/mods.toml") {
        expand properties
    }

    // Replace description in pack.mcmeta
    filesMatching("pack.mcmeta") {
        expand properties
    }
}

loom {
    forge {
    }
}

shadowJar {
    exclude "fabric.mod.json"
    exclude "architectury.common.json"

    configurations = [project.configurations.shadowCommon]
    archiveClassifier = "dev-shadow"
}

remapJar {
    input.set shadowJar.archiveFile
    dependsOn shadowJar
    archiveClassifier = null
}

jar {
    archiveClassifier = "dev"
}

sourcesJar {
    def commonSources = project(":common").sourcesJar
    dependsOn commonSources
    from commonSources.archiveFile.map { zipTree(it) }
}

components.java {
    withVariantsFromConfiguration(project.configurations.shadowRuntimeElements) {
        skip()
    }
}
kotlin {
    jvmToolchain(17)
}